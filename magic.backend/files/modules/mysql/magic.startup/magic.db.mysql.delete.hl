
/*
 * Deletes one specified record from your database.
 *
 * Arguments
 *
 * - [database] - Database to delete a record from
 * - [table] - Table to delete a record from
 * - [ids] - A list of IDs that uniquely identifies your record
 */
slot:magic.db.mysql.delete
   .arguments
      database:string
      table:string
      ids
   .lambda


      /*
       * Sanity checking invocation by making sure caller
       * provided an ID at all, to avoid deleting entire table.
       */
      if
         eq
            get-count:x:@.arguments/*/ids/*
            :int:0
         .lambda
            throw:No IDs given to [magic.db.mysql.delete]


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/database


         /*
          * Parametrizing [mysql.delete].
          */
         add:x:./*/mysql.delete/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*


         /*
          * Invoking [mysql.delete].
          */
         mysql.delete
            table:x:@.arguments/*/table
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            deleted-records:x:@mysql.delete

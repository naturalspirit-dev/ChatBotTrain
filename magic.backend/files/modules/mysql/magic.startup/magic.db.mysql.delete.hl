
/*
 * Deletes the specified [id] record from your [connection]/[table].
 */
slot:magic.db.mysql.delete
   .lambda


      /*
       * Sanity checking invocation.
       * Notice, a sane id node from [ids] must not be null, and not "".
       */
      if
         eq
            get-count:x:@.arguments/*/ids/*
            .:int:0
         .lambda
            throw:No ID given to [magic.db.delete]
      for-each:x:@.arguments/*/ids/*
         if
            or
               eq
                  get-value:x:@.dp/#
                  .
               eq
                  get-value:x:@.dp/#
                  .:
            .lambda
               throw:[magic.db.delete] was not given a valid ID


      /*
       * Opening up a connection.
       */
      .connection
      set-value:x:@.connection
         concat
            :[
            get-value:x:@.arguments/*/database
            :]
      mysql.connect:x:@.connection


         /*
          * Parametrizing [mysql.delete].
          */
         add:x:./*/mysql.delete/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*


         /*
          * Invoking [mysql.delete].
          */
         mysql.delete
            table:x:@.arguments/*/table
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            deleted-records:x:@mysql.delete


/*
 * Creates a new item in your [database]/[table] with the specified [values].
 */
slot:magic.db.mysql.create
   .arguments
      database:string
      table:string
      values
      exclude
   .lambda


      /*
       * Opening up a connection.
       */
      .connection
      set-value:x:@.connection
         concat
            :[
            get-value:x:@.arguments/*/database
            :]
      mysql.connect:x:@.connection


         /*
          * Parametrizing invocation to [mysql.create] with [values].
          */
         add:x:./*/mysql.create/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Then adding [exclude] if given.
          */
         if
            mt
               get-count:x:@.arguments/*/exclude/*
               .:int:0
            .lambda


               /*
                * Adding [exclude] to [mysql.create].
                */
               add:x:../*/mysql.connect/*/mysql.create
                  .
                     exclude
               add:x:../*/mysql.connect/*/mysql.create/*/exclude
                  get-nodes:x:@.arguments/*/exclude/*


         /*
          * Invoking [mysql.create].
          */
         mysql.create
            table:x:@.arguments/*/table
            values


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            id:x:@mysql.create

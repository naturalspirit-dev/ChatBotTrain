
/*
 * Creates a new item in your [database]/[table] with the specified [values],
 * excluding everything from [exclude].
 *
 * Arguments
 *
 * - [database] - Database to create a record in
 * - [table] - Table to create your record in
 * - [values] - A column/value collection for your new record
 */
slot:magic.db.mysql.create
   .arguments
      database:string
      table:string
      values
   .lambda


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/database


         /*
          * Parametrizing invocation to [mysql.create] with [values].
          */
         add:x:./*/mysql.create/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Invoking [mysql.create].
          */
         mysql.create
            table:x:@.arguments/*/table
            values


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            id:x:@mysql.create

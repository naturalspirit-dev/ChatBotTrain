
/*
 * Creates the [magic.mysql.update] slot.
 *
 * Assumes one column with one value, which it will use as its criteria
 * parameter when updating your item from your database. The new values can
 * be supplied in [values].
 */
slot:magic.mysql.update


   .arguments
      connection:string
      table:string
      query
      values


   .lambda


      /*
       * Dynamically builds our SQL, making sure we guard against SQL injection.
       */
      .query
      set-value:x:@.query
         concat
            .:"update `"
            get-value:x:@.arguments/*/table
            .:"` set "


      /*
       * Looping through each [values] specified.
       */
      .idx:int:0
      for-each:x:@.arguments/*/values/*


         /*
          * Making sure we separate each [values] by a "," in SQL text.
          */
         if
            mt
               get-value:x:@.idx
               .:int:0
            .lambda
               set-value:x:@.query
                  concat
                     get-value:x:@.query
                     .:","


         /*
          * Appending column name into SQL command text.
          */
         set-value:x:@.query
            concat
               get-value:x:@.query
               .:"`"
               get-name:x:@.dp/#
               .:"` = @"
               get-value:x:@.idx


         /*
          * Appending argument parameter into [.query]
          */
         set-name:x:+/+/*/*
            concat
               .:"@"
               get-value:x:@.idx
         set-value:x:+/*/*
            get-value:x:@.dp/#
         add:x:@.query
            .
               foo:bar


         /*
          * Incrementing [.idx].
          */
         set-value:x:@.idx
            +
               get-value:x:@.idx
               .:int:1


      /*
       * Appending where clause.
       */
      set-value:x:@.query
         concat
            get-value:x:@.query
            .:" where `"
            get-name:x:@.arguments/*/query/*
            .:"` = @id"
      unwrap:x:+/*/*
      add:x:@.query
         .
            @id:x:@.arguments/*/query/*


      /*
       * Executing SQL command.
       */
      set-value:x:./*/mysql.connect
         get-value:x:@.arguments/*/connection
      mysql.connect
         add:x:./*/mysql.execute
            get-nodes:x:@.query/*
         mysql.execute:x:@.query
         unwrap:x:+/*
         return-nodes
            updated-records:x:@mysql.execute
      

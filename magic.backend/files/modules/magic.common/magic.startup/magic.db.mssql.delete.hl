
/*
 * Deletes the specified [id] record from your [connection]/[table].
 */
slot:magic.db.mssql.delete
   .lambda


      /*
       * Sanity checking invocation.
       * Notice, a sane id node from [ids] must not be null, and not "".
       */
      for-each:x:@.arguments/*/ids/*
         if
            or
               eq
                  get-value:x:@.dp/#
                  .
               eq
                  get-value:x:@.dp/#
                  .:
            .lambda
               throw:[magic.db.delete] was not given a valid ID


      /*
       * Opening up a connection.
       */
      mssql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing [mssql.delete].
          */
         add:x:./*/mssql.delete/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*


         /*
          * Invoking [mssql.delete].
          */
         mssql.delete
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            deleted-records:x:@mssql.delete

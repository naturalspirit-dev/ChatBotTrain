
/*
 * Updates the specified [ids] item in your [connection]/[table] with the
 * values from [values]. [ids] can be a collection of values, preferably the IDs of your table,
 * that defines which item(s) to update. All [ids] are mandatory, and reuiqred to have values.
 */
slot:magic.db.mssql.update
   .lambda


      /*
       * Sanity checking invocation.
       */
      for-each:x:@.arguments/*/ids/*
         if
            or
               eq
                  get-value:x:@.dp/#
                  .
               eq
                  get-value:x:@.dp/#
                  .:
            .lambda
               throw:[magic.db.update] was not given a valid ID


      /*
       * Opening up a connection.
       */
      mssql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing [mssql.update], first with the [where] parts,
          * extracted form [ids].
          */
         add:x:./*/mssql.update/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*
         add:x:./*/mssql.update/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Then adding [exclude] if given.
          */
         if
            mt
               get-count:x:@.arguments/*/exclude/*
               .:int:0
            .lambda


               /*
                * Adding [exclude] to [mssql.update].
                */
               add:x:../*/mssql.connect/*/mssql.update
                  .
                     exclude
               add:x:../*/mssql.connect/*/mssql.update/*/exclude
                  get-nodes:x:@.arguments/*/exclude/*


         /*
          * Invoking [mssql.update].
          */
         mssql.update
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table
            values
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            updated-records:x:@mssql.update

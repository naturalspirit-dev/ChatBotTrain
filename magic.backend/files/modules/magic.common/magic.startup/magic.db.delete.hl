
/*
 * Deletes the specified [id] record from your [connection]/[table].
 */
slot:magic.db.delete
   .lambda


      /*
       * Sanity checking invocation.
       */
      for-each:x:@.arguments/*/ids/*
         if
            eq
               get-value:x:@.dp/#
               .
            .lambda
               throw:[magic.db.delete] was not given a valid ID


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing [mysql.delete].
          */
         add:x:./*/mysql.delete/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*


         /*
          * Invoking [mysql.delete].
          */
         mysql.delete
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            deleted-records:x:@mysql.delete

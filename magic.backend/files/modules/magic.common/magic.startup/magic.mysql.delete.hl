
/*
 * Creates the [magic.mysql.delete] slot.
 *
 * Assumes one column with one value in [query], which it will use as
 * its criteria parameter when deleting your item from your database.
 */
slot:magic.mysql.delete


   .arguments
      connection:string
      table:string
      query


   .lambda


      /*
       * Dynamically builds our SQL, making sure we guard against SQL injection.
       */
      .query
      set-value:x:@.query
         concat
            .:"delete from `"
            get-value:x:@.arguments/*/table
            .:"` where `"
            get-name:x:@.arguments/*/query/0
            .:"` = @id"
      unwrap:x:+/*/*
      add:x:@.query
         .
            @id:x:@.arguments/*/query/0
      set-value:x:./*/mysql.connect
         get-value:x:@.arguments/*/connection
      mysql.connect
         add:x:./*/mysql.execute
            get-nodes:x:@.query/*
         mysql.execute:x:@.query
         unwrap:x:+/*
         return-nodes
            deleted-records:x:@mysql.execute



/*
 * Creates the [magic.mysql.read] slot.
 */
slot:magic.mysql.read
   .arguments
      connection:*
      table:*
      query:*
      limit:*
      offset:*
   .lambda
      set-value:x:./*/mysql.connect
         get-value:x:@.arguments/*/connection
      .query:"select * from "
      set-value:x:@.query
         concat
            get-value:x:@.query
            get-value:x:@.arguments/*/table
      if
         mt
            get-count:x:@.arguments/*/query/*
            .:int:0
         .lambda
            set-value:x:@.query
               concat
                  get-value:x:@.query
                  .query:" where":
            .idx:int:0
            for-each:x:@.arguments/*/query/*
               if
                  not
                     eq
                        get-value:x:@.idx
                        .:int:0
                  .lambda
                     set-value:x:@.query
                        concat
                           get-value:x:@.query
                           .:" and"
               set-value:x:@.query
                  concat
                     get-value:x:@.query
                     .:" "
                     get-name:x:@.dp/#
                     .:" = @"
                     get-value:x:@.idx
               set-name:x:+/+/*/*
                  concat
                     .:"@"
                     get-value:x:@.idx
               set-value:x:+/*/*
                  get-value:x:@.dp/#
               add:x:@.query
                  .
                     foo:bar
               set-value:x:@.idx
                  +
                     get-value:x:@.idx
                     .:int:1

      if
         mt
            get-count:x:@.arguments/*/limit
            .:int:0
         .lambda
            set-value:x:@.query
               concat
                  get-value:x:@.query
                  .:" limit "
                  get-value:x:@.arguments/*/limit

      if
         mt
            get-count:x:@.arguments/*/offset
            .:int:0
         .lambda
            set-value:x:@.query
               concat
                  get-value:x:@.query
                  .:" offset "
                  get-value:x:@.arguments/*/offset

      mysql.connect
         add:x:+
            get-nodes:x:@.query/*
         mysql.select:x:@.query
         return-nodes:x:-/*


/*
 * Read items from your [connection]/[table] according to [values] supplied.
 */
slot:magic.db.read
   .lambda


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing invocation to [mysql.read].
          */
         if
            mt
               get-count:x:@.arguments/*/where/*
               .:int:0
            .lambda
               add:x:../*/mysql.connect/*/mysql.read
                  .
                     where
                        and
               add:x:../*/mysql.connect/*/mysql.read/*/where/*/and
                  get-nodes:x:@.arguments/*/where/*


         /*
          * Adding [limit], [offset], [order] and [direction]
          */
         add:x:./*/mysql.read
            get-nodes:x:@.arguments/*/limit
         add:x:./*/mysql.read
            get-nodes:x:@.arguments/*/offset
         add:x:./*/mysql.read
            get-nodes:x:@.arguments/*/order
         add:x:./*/mysql.read
            get-nodes:x:@.arguments/*/direction


         /*
          * Invoking [mysql.read].
          */
         mysql.read
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table


         /*
          * Returning results to caller.
          */
         return-nodes:x:@mysql.read/*

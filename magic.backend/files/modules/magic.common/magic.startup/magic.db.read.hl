
/*
 * Read items from your [connection]/[table] according to [values] supplied.
 */
slot:magic.db.read
   .lambda


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing invocation to [mysql.select].
          */
         if
            mt
               get-count:x:@.arguments/*/where/*
               .:int:0
            .lambda
               add:x:../*/mysql.connect/*/mysql.select
                  .
                     where
                        and
               add:x:../*/mysql.connect/*/mysql.select/*/where/*/and
                  get-nodes:x:@.arguments/*/where/*


         /*
          * Adding [limit] and [offset]
          */
         add:x:./*/mysql.select
            get-nodes:x:@.arguments/*/limit
         add:x:./*/mysql.select
            get-nodes:x:@.arguments/*/offset


         /*
          * Invoking [mysql.select].
          */
         mysql.select
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table


         /*
          * Returning results to caller.
          */
         return-nodes:x:@mysql.select/*

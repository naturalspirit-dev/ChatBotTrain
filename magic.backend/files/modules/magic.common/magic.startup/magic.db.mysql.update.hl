
/*
 * Updates the specified [ids] item in your [connection]/[table] with the
 * values from [values]. [ids] can be a collection of values, preferably the IDs of your table,
 * that defines which item(s) to update. All [ids] are mandatory, and reuiqred to have values.
 */
slot:magic.db.mysql.update
   .lambda


      /*
       * Sanity checking invocation.
       */
      for-each:x:@.arguments/*/ids/*
         if
            or
               eq
                  get-value:x:@.dp/#
                  .
               eq
                  get-value:x:@.dp/#
                  .:
            .lambda
               throw:[magic.db.update] was not given a valid ID


      /*
       * Opening up a connection.
       */
      mysql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing [mysql.update], first with the [where] parts,
          * extracted form [ids].
          */
         add:x:./*/mysql.update/*/where/*/and
            get-nodes:x:@.arguments/*/ids/*
         add:x:./*/mysql.update/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Then adding [exclude] if given.
          */
         if
            mt
               get-count:x:@.arguments/*/exclude/*
               .:int:0
            .lambda


               /*
                * Adding [exclude] to [mysql.update].
                */
               add:x:../*/mysql.connect/*/mysql.update
                  .
                     exclude
               add:x:../*/mysql.connect/*/mysql.update/*/exclude
                  get-nodes:x:@.arguments/*/exclude/*


         /*
          * Parametrizing invocation to [mysql.update] with the new [values].
          */
         add:x:./*/mysql.update/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Invoking [mysql.update].
          */
         mysql.update
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table
            values
            where
               and


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            updated-records:x:@mysql.update

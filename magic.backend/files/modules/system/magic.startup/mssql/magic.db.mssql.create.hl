
/*
 * Creates a new item in your [connection]/[table] with the specified [values]
 * column values.
 */
slot:magic.db.mssql.create
   .lambda


      /*
       * Opening up a connection.
       */
      mssql.connect:x:@.arguments/*/connection


         /*
          * Parametrizing invocation to [mssql.create] with [values].
          */
         add:x:./*/mssql.create/*/values
            get-nodes:x:@.arguments/*/values/*


         /*
          * Then adding [exclude] if given.
          */
         if
            mt
               get-count:x:@.arguments/*/exclude/*
               .:int:0
            .lambda


               /*
                * Adding [exclude] to [mssql.create].
                */
               add:x:../*/mssql.connect/*/mssql.create
                  .
                     exclude
               add:x:../*/mssql.connect/*/mssql.create/*/exclude
                  get-nodes:x:@.arguments/*/exclude/*


         /*
          * Invoking [mssql.create].
          */
         mssql.create
            connection:x:@.arguments/*/connection
            table:x:@.arguments/*/table
            values


         /*
          * Returning results to caller.
          */
         unwrap:x:+/*
         return-nodes
            id:x:@mssql.create


/*
 * Sets up authentication of system according to the arguments provided.
 */
.arguments
   databaseType:string
   rootUsername:string
   rootPassword:string


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Verifying that system haven't already been setup with some secure
 * authentication scheme previously.
 */
slots.get:magic.authenticate
if
   or
      not
         exists:x:@slots.get/*/.is-default-auth
      not
         eq
            get-value:x:@slots.get/*/.is-default-auth
            .:bool:true
   .lambda
      throw:Your system has already been configured to use a secure authentication scheme


/*
 * Evaluating SQL script that creates our database, but first we must dynamically
 * determine which script to execute.
 */
strings.concat
   .:/misc/
   get-value:x:@.arguments/*/databaseType
   .:/magic_auth.sql


/*
 * Loading file, and executing it.
 */
io.files.load:x:@strings.concat
set-name:x:../*/xxx.connect
   strings.concat
      get-value:x:@.arguments/*/databaseType
      .:.connect
set-name:x:../**/xxx.execute
   strings.concat
      get-value:x:@.arguments/*/databaseType
      .:.execute


/*
 * This part will be replaced dynamically according to the [databaseType] argument.
 */
xxx.connect
   xxx.execute:x:@io.files.load


/*
 * Inserting the root user according to arguments given.
 */
strings.concat
   .:/misc/
   get-value:x:@.arguments/*/databaseType
   .:/create-user.hl
io.files.load:x:@strings.concat
hyper2lambda:x:-
unwrap:x:+/*/*
add:x:@hyper2lambda/*/.arguments
   .
      username:x:@.arguments/*/rootUsername
      password:x:@.arguments/*/rootPassword
      roles
         .:root
eval:x:@hyper2lambda


/*
 * Now we have created our database, and our initial root user, and we can start moving files around,
 * and modifying slots accordingly.
 */
io.files.move:/modules/system/magic.startup/magic.authenticate.hl
   .:/trash/magic.authenticate.hl
strings.concat
   .:/misc/
   get-value:x:@.arguments/*/databaseType
   .:/magic.authenticate.hl
strings.concat
   .:/misc/
   get-value:x:@.arguments/*/databaseType
   .:/magic.authenticate.hl
io.files.move:x:-
   .:/modules/system/magic.startup/magic.authenticate.hl
io.files.load:/modules/system/magic.startup/magic.authenticate.hl


/*
 * Evaluating new authentication file, to update our slot.
 */
eval:x:@io.files.load


/*
 * Logging in user again, using the specified credentials,
 * and returning a new JWT token to caller.
 */
unwrap:x:+/*
slots.signal:magic.authenticate
   username:x:@.arguments/*/rootUsername
   password:x:@.arguments/*/rootPassword


/*
 * Returning success to caller.
 */
slots.return-nodes:x:@slots.signal/*

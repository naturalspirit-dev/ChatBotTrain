
/*
 * Ping endpoint, allowing for anonymous access, 
 * simply to verify server is up and running.
 *
 * Notice, if an authenticated user invokes this endpoint,
 * it will return its username and the user's roles.
 *
 * The endpoint will also return the version of its server side bindings.
 */


/*
 * Returning version of server side and the authenticated user, if any.
 */
version
auth.ticket.get


/*
 * Checking if user is authenticated, at which point we returns username + user's roles.
 */
if
   not
      eq
         get-value:x:@auth.ticket.get
         .
   .lambda
      add:x:+/+/*/*/*/roles
         get-nodes:x:@auth.ticket.get/*
      unwrap:x:+/*/*/user/*/username
      add:x:../*/slots.return-nodes
         .
            user
               username:x:@auth.ticket.get
               roles


      /*
       * Checking if user is authenticated as root,
       * at which point we check to see if he has changed the default
       * JWT secret/salt, and if not, return a warning.
       */
      if
         exists:x:@auth.ticket.get/*/=root
         .lambda


            /*
             * Creating a default [warnings] container, which will contain any warnings we can find by
             * traversing meta data in the system.
             */
            add:x:../*/slots.return-nodes
               .
                  warnings


            /*
             * User is authenticated as root.
             * Making sure we check some system settings, to create warning on client side,
             * for unfinished configurations, etc.
             *
             * First we check if root user has overridden the default JWT secret.
             */
            config.get:"magic:auth:secret"
            if
               eq
                  get-value:x:@config.get
                  .:THIS-IS-NOT-A-GOOD-SECRET-PLEASE-CHANGE-IT
               .lambda
                  add:x:../*/slots.return-nodes/*/warnings
                     .
                        jwt:Please change your auth secret in your appSettings.json file


            /*
             * Then we check to see if user is still using the default authentication slot.
             */
            slots.get:magic.authenticate
            if
               eq
                  get-value:x:@slots.get/*/.is-default-auth
                  .:bool:true
               .lambda
                  add:x:../*/slots.return-nodes/*/warnings
                     .
                        defaultAuth:Please secure your authentication scheme by overriding the [magic.authenticate] slot


/*
 * Returning version, success, plus optionally also authenticated user + roles.
 */
unwrap:x:+/*/version
slots.return-nodes
   result:success
   version:x:@version

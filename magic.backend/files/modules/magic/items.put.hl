
/*
 * Updates the specified [id] item.
 *
 * Arguments
 *
 * - [id] - Id of item to update, mandatory
 * - [subject] - New subject for your item, optionally
 * - [done] - New done status for your item, optionally
 */
.arguments
   id:long
   subject:string
   done:int


/*
 * Parametrizing [magic.db.mysql.update], first its [ids].
 */
unwrap:x:./*/signal/*/ids/*
remove-node:x:@.arguments/*/id


/*
 * Adding the rest of our parameters into [values].
 */
add:x:./*/signal/*/values
   get-nodes:x:@.arguments/*


/*
 * Invoking [magic.db.mysql.update]
 */
signal:magic.db.mysql.update
   connection:signal:@"config:""Databases:magic"""
   table:items
   values
   ids
      id:x:@.arguments/*/id
   exclude
      id


/*
 * Returning results back to caller.
 */
return-nodes:x:@signal/*

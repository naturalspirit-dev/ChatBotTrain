
// Connecting to database.
wait.mysql.connect:magic_mail

   // Connecting to pop3 server.
   wait.mail.pop3.fetch
      .lambda

         // Lambda object evaluated once for every email fetched from pop3.
         unwrap:x:+/*
         .content
            from:x:@.message/*/from/0
            subject:x:@.message/*/subject
            body:x:@.message/**/entity/**/content/[0,1]

         // Checking if contact exists, and if not, inserting new contact.
         .sender-id
         wait.set-value:x:@.sender-id
            wait.mysql.scalar:select id from contacts where email = @email
               @email:x:@.content/*/from
         wait.if
            eq
               get-value:x:@.sender-id
               .
            .lambda
               wait.set-value:x:@.sender-id
                  wait.mysql.scalar:insert into contacts (email) values (@email); select last_insert_id();
                     @email:x:@.content/*/from

         // Inserting into emails.
         wait.mysql.scalar:"insert into emails (subject, body, direction) values (@subject, @body, 'received'); select last_insert_id();"
            @subject:x:@.content/*/subject
            @body:x:@.content/*/body

         // Associating email with sender (contacts).
         wait.mysql.execute:insert into emails_contacts (email_id, contact_id) values (@email, @contact)
            @email:x:@wait.mysql.scalar
            @contact:x:@.sender-id

// How to compare?

/*
 * Comparisons are often combined with [if], [else-if] and [while] to
 * conditionally execute some [.lambda] object only if some value reference
 * through an expression correctly compares to some other value. This allows
 * you to add logic such as 'execute [.lambda] if value of [.foo] is 5 or less'.
 *
 * In the code below we are executing the [.lambda] object, but only if [.src]
 * is less than the integer value of 5.
 */

// Our variable.
.src:int:4

// Creating a log entry if [.src] is 5 or less.
if
   lte:x:@.src
      .:int:5
   .lambda

      // Creating a log entry.
      log.info:[.src] was 5 or less

/*
 * There are many comparison slots in Hyperlambda. Below you can find
 * some of the most common.
 *
 * - [eq] - Equals to
 * - [neq] - Not equal to
 * - [lt] - Less than
 * - [mt] - More than
 * - [lte] - Less than or equal to
 * - [mte] - More than or equal to
 */


/*
 * Upload file endpoint template file.
 *
 * This file creates an HTTP PUT endpoint that allows you to upload a single file file as multipart/form-data
 * and stores it in a specified folder on the server.
 *
 * NOTICE! This file should probably be consumed as a Macro, and not as a template, since this
 * will correctly decorate your file with the correct properties.
 */
.description:Upload file endpoint
.arguments
   file:*
   old_file:string
.type:file-upload
.accept:multipart/form-data

/*
 * Verifying user has access to invoke endpoint.
 */
auth.ticket.verify:[[auth]]

/*
 * Folder where file should be saved.
 */
.folder
set-value:x:@.folder
   strings.concat
      .:/etc/
      .:[[module]]
      .:/

/*
 * Verifying the folder exists, and if not, creating it.
 */
if
   not
      io.folder.exists:x:@.folder
   .lambda

      // Need to create folder.
      io.folder.create:x:@.folder

else

   /*
    * Checking if [old_file] is specified, and existing.
    */
   .old-file-full-path
   set-value:x:@.old-file-full-path
      strings.concat
         get-value:x:@.folder
         get-value:x:@.arguments/*/old_file
   if
      and
         not
            eq
               get-value:x:@.arguments/*/old_file
               .
         not
            eq
               get-value:x:@.arguments/*/old_file
               .:
         io.file.exists:x:@.old-file-full-path
      .lambda

         // Old file exists, deleting it.
         io.file.delete:x:@.old-file-full-path

/*
 * Saving file with the specified filename to the specified folder.
 */
strings.concat
   get-value:x:@.folder
   get-value:x:@.arguments/*/file/*/name
io.stream.save-file:x:@strings.concat
   get-value:x:@.arguments/*/file/*/stream

/*
 * Returning success to caller.
 */
return
   result:success


/*
 * Macro for Hyper IDE that creates a 'magic.startup' folder for you within your module.
 */
.name:Startup wrapper
.description:"Macro for Hyper IDE that creates a 'magic.startup' folder for you within your module"
.arguments
   .
      name:module
      type:string
      description:"The name of your module, which will become its root folder name"
      mandatory:bool:true
   .
      name:database
      type:string
      description:"The name of your app's database"
      mandatory:bool:false
   .
      name:mssql
      type:multi_string
      description:"Microsoft SQL server create database script"
      mandatory:bool:false
   .
      name:mysql
      type:multi_string
      description:"MySQL create database script"
      mandatory:bool:false
.lambda

   /*
    * Sanity checking invocation.
    */
   validators.mandatory:x:@.arguments/*/module
   validators.regex:x:@.arguments/*/module
      regex:^[a-zA-Z0-9_-]+$

   /*
    * Figuring out module's complete path.
    */
   .module-path
   set-value:x:@.module-path
      strings.concat
         .:/modules/
         get-value:x:@.arguments/*/module
         .:/

   /*
    * Making sure that module exists.
    */
   if
      not
         io.folder.exists:x:@.module-path
      .lambda

         // No such module
         strings.concat
            .:"The module called '"
            get-value:x:@.arguments/*/module
            .:"' does not exist"
         throw:x:-
            public:bool:true
            status:int:404

   /*
    * Making sure that module does not already have a 'magic.startup' folder.
    */
   strings.concat
      get-value:x:@.module-path
      .:/magic.startup/
   if
      io.folder.exists:x:@strings.concat
      .lambda

         // No such module
         strings.concat
            .:"The module called '"
            get-value:x:@.arguments/*/module
            .:"' already has a 'magic.startup' folder and hence we cannot create another"
         throw:x:-
            public:bool:true
            status:int:404

   /*
    * Copying main 'magic.startup' template folder from misc folder.
    */
   io.folder.copy:/misc/ide/template-folders/magic.startup/
      get-value:x:@.module-path

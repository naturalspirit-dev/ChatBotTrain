
/*
 * Evaluates the given [hyperlambda], and returns
 * the results of the evaluation.
 *
 * Arguments
 *
 * - [hyperlambda] - Mandatory, Hyperlambda you want to evaluate
 */
.arguments
   hyperlambda:string
.description:"Evaluates the given [hyperlambda], and returns the results of the evaluation."


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Transforms the specified [hyperlambda] to a lambda object,
 * making sure we correctly set the root node for the execution
 * in the process. This looks a bit cheezy, but to make sure
 * the root node for the execution object is the node containing
 * the Hyperlambda as children, it is necessary.
 */
.x:node:""
hyper2lambda:x:@.arguments/*/hyperlambda
add:x:@.x/#
   get-nodes:x:@hyper2lambda/*
eval:x:@.x/#


/*
 * Returning the results of the evaluation to caller.
 */
lambda2hyper:x:@.x/#/*
unwrap:x:+/*
return-nodes
   result:x:@lambda2hyper

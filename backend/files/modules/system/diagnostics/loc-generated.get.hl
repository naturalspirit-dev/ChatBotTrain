
/*
 * Returns Lines Of Code (LOC) generated in total by Magic.
 */
.description:Returns lines of code (LOC) generated by Magic


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Retrieving items from database.
 */
.total-backend:int:0
.total-frontend:int:0
data.connect:magic
   data.select:"select content from log_entries where content like 'Loc generated %'"
   for-each:x:-/*/*
      strings.split:x:@.dp/#
         .:"'"
      if
         strings.contains:x:@.dp/#
            .:"'backend'"
         .lambda
            math.increment:x:@.total-backend
               convert:x:@strings.split/1
                  type:int
      else
         math.increment:x:@.total-frontend
            convert:x:@strings.split/1
               type:int
unwrap:x:+/*
return
   backend:x:@.total-backend
   frontend:x:@.total-frontend

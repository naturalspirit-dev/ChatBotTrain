
/*
 * Template for HTTP GET CRUD requests.
 */
.arguments
   limit:long
   offset:long
   order:string
   direction:string
   operator:string
.description:Returns items from your {table} table in your {database} database according to the specified arguments
.type:crud-read


/*
 * Sanity checking invocation.
 */
validators.enum:x:@.arguments/*/operator
   .:or
   .:and


/*
 * Checking if user supplied an operator, and if so,
 * changing the boolean operator for comparison operations.
 */
if
   exists:x:@.arguments/*/operator
   .lambda

      /*
       * User provided a boolean comparison operator.
       */
      set-name:x:../*/data.connect/*/data.read/*/where/*
         get-value:x:@.arguments/*/operator


/*
 * Invoking read, making sure we add any [where] conditions, and
 * other conditions, such as paging and order conditions.
 */
data.connect:{database}
   database-type:{database-type}
   add:x:./*/data.read
      get-nodes:x:@.arguments/*/limit
      get-nodes:x:@.arguments/*/offset
      get-nodes:x:@.arguments/*/order
      get-nodes:x:@.arguments/*/direction
   remove-nodes:x:@.arguments/*/operator
   remove-nodes:x:@.arguments/*/limit
   remove-nodes:x:@.arguments/*/offset
   remove-nodes:x:@.arguments/*/order
   remove-nodes:x:@.arguments/*/direction
   add:x:+/*/where/*
      get-nodes:x:@.arguments/*
   data.read
      database-type:{database-type}
      table:{table}
      columns
      where
         and
   return-nodes:x:@data.read/*

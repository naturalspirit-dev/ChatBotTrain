
/*
 * Lists all database in your installation.
 */
.arguments
   databaseType:string
.description:"Returns all databases in your installation, according to the databaseType specified."


/*
 * Sanity checks invocation.
 */
validators.mandatory:x:@.arguments/*/databaseType


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Invokes [magic.db.xxx.databases] slot, that's responsible
 * for returning our database names according to what database type
 * caller specified.
 */
strings.concat
   .:magic.db.
   get-value:x:@.arguments/*/databaseType
   .:.databases
signal:x:@strings.concat


/*
 * Making sure we only return [to-lower] names to caller, to
 * avoid creating CRUD endpoints with CAPS characters, which can
 * create confusion when moving modules between Windows and Linux/OSX.
 */
for-each:x:@signal/*/*
   set-value:x:@.dp/#
      strings.to-lower:x:@.dp/#


/*
 * Returning database to caller.
 */
return-nodes:x:@signal/*

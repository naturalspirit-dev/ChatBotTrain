
/*
 * Lists all tables in your specified [database].
 */
.arguments
   databaseType:string
   database:string
.description:Returns all tables in your specified database


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Sanity checks invocation.
 */
validators.mandatory:x:@.arguments/*/databaseType
validators.mandatory:x:@.arguments/*/database
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:mssql


/*
 * Invokes [magic.db.xxx.tables] slot, that's responsible
 * for returning our table names according to what database type
 * caller specified, and which database he specified.
 */
strings.concat
   .:magic.db.
   get-value:x:@.arguments/*/databaseType
   .:.tables
unwrap:x:+/*
signal:x:@strings.concat
   database:x:@.arguments/*/database


/*
 * Returning tables to caller.
 */
return-nodes:x:@signal/*

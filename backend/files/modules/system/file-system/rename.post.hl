
/*
 * Renames the specified file or folder.
 */
.arguments
   oldName:string
   newName:string
.description:Renames the specified file or folder


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Basic logging.
 */
log.info
   .:"Renaming '"
   get-value:x:@.arguments/*/oldName
   .:"' to '"
   get-value:x:@.arguments/*/newName
   .:"'"


/*
 * Checking if specified object is a file or a folder.
 */
if
   io.folder.exists:x:@.arguments/*/oldName
   .lambda


      /*
       * File object is a folder, renaming it.
       */
      strings.split:x:@.arguments/*/oldName
         .:/
      remove-nodes:x:-/0/-
      strings.join:x:@strings.split/*
         .:/
      io.folder.move:x:@.arguments/*/oldName
         strings.concat
            .:/
            get-value:x:@strings.join
            .:/
            get-value:x:@.arguments/*/newName
            .:/

else


   /*
    * File object is a file, renaming it.
    */
   io.file.move:x:@.arguments/*/oldName
      strings.concat
         io.path.get-folder:x:@.arguments/*/oldName
         get-value:x:@.arguments/*/newName


/*
 * Returning result to caller.
 */
return-nodes
   result:success

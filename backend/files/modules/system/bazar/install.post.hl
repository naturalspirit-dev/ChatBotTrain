
/*
 * Installs the specified module.
 */
.arguments
   url:string
   name:string
.description:Installs the specified Bazarmodule


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/url
validators.mandatory:x:@.arguments/*/name


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Downloading zip file.
 */
http.get.binary:x:@.arguments/*/url


/*
 * Saving file to modules folder.
 */
strings.split:x:@http.get.binary/*/headers/*/Content-Disposition
   .:=
strings.concat
   .:/temp/
   get-value:x:@strings.split/1
io.file.save.binary:x:@strings.concat
   get-value:x:@http.get.binary/*/content


/*
 * Unzipping downloaded ZIP file.
 */
strings.concat
   .:/modules/
   get-value:x:@.arguments/*/name
io.file.unzip:x:@strings.concat/@strings.concat
   folder:x:@strings.concat


/*
 * Returning success to caller.
 */
return
   result:success

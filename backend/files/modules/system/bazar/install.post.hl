
/*
 * Installs the specified Bazar module.
 */
.arguments
   url:string
   name:string
.description:Installs the specified Bazar module


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Sanity checking invocation.
 */
validators.mandatory:x:@.arguments/*/url
validators.mandatory:x:@.arguments/*/name


/*
 * Verifying that folder doesn't already exist, as in that module
 * is already installed.
 */
strings.concat
   .:/modules/
   get-value:x:@.arguments/*/name
   .:/
if
   io.folder.exists:x:@strings.concat
   .lambda

      /*
       * Oops ...
       */
      throw:Module is already installed. If you wish to update the module please uninstall it first.
         public:bool:true
         status:int:500


/*
 * Downloading zip file.
 */
http.get.binary:x:@.arguments/*/url


/*
 * Saving file to modules folder.
 */
strings.split:x:@http.get.binary/*/headers/*/Content-Disposition
   .:=
strings.concat
   .:/modules/
   get-value:x:@strings.split/1
io.file.save.binary:x:@strings.concat
   get-value:x:@http.get.binary/*/content


/*
 * Unzipping downloaded ZIP file.
 */
io.file.unzip:x:@strings.concat


/*
 * Deletes ZIP file.
 */
io.file.delete:x:@strings.concat


/*
 * Renaming the folder to the name specified by caller.
 */
strings.split:x:@strings.concat
   .:.zip
io.folder.move:x:@strings.split/0
   strings.concat
      .:/modules/
      get-value:x:@.arguments/*/name
      .:/


/*
 * Returning success to caller.
 */
return
   result:success

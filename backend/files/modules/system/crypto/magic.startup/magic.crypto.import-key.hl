
/*
 * Imports the specified public key.
 */
slots.create:magic.crypto.import-key


   /*
    * Figuring out key's fingerprint.
    */
   crypto.fingerprint:x:@.arguments/*/content


   /*
    * Checking if caller supplied en enabled argument,
    * at which point we use it, instead of defaulting its
    * value to false.
    */
   .enabled:bool:false
   if
      exists:x:@.arguments/*/enabled
      .lambda
         set-value:x:@.enabled
            get-value:x:@.arguments/*/enabled


   /*
    * Importing key into database.
    */
   data.connect:magic


      /*
       * Checking if key exists in database from before.
       */
      data.read
         table:crypto_keys
         where
            and
               fingerprint.eq:x:@crypto.fingerprint
      if
         exists:x:@data.read/*/*
         .lambda


            /*
             * Oops, key already exists in database.
             */
            throw:Key has already been imported
               public:true
               status:400


      /*
       * Inserting key into database.
       */
      data.create
         table:crypto_keys
         values
            subject:x:@.arguments/*/subject
            email:x:@.arguments/*/email
            domain:x:@.arguments/*/domain
            type:RSA
            fingerprint:x:@crypto.fingerprint
            content:x:@.arguments/*/content
            enabled:x:@.enabled
            vocabulary:@"add
return
get-nodes
vocabulary
slots.vocabulary
"


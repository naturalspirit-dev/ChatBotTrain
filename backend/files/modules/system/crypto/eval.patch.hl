
/*
 * Evaluates a cryptographically signed HTTP REST invocation.
 */
.description:"Evaluates a cryptographically signed HTTP REST invocation."


/*
 * Retrieving signing key's fingerprint, for then to retrieve
 * the public key associated with the fingerprint.
 */
crypto.get-key:x:@.arguments/*/body
unwrap:x:+/*
signal:magic.crypto.get-public-key
   fingerprint:x:@crypto.get-key


/*
 * Verifying signature.
 */
try

   /*
    * Verifies signature, and also chops up payload.
    */
   crypto.verify:x:@.arguments/*/body
      public-key:x:@signal/*/public_key


   /*
    * Invoking Hyperlambda specified by caller.
    */
   add:x:./*/whitelist/*/.lambda
      hyper2lambda:x:@crypto.verify
   add:x:./*/whitelist/*/vocabulary
      get-nodes:x:@signal/*/vocabulary/*
   whitelist
      vocabulary
      .lambda

   /*
    * Returning result of execution.
    */
   unwrap:x:./*/return
   add:x:./*/return
      get-nodes:x:@whitelist/*
   return:x:@whitelist

.catch

   /*
    * Execution error occurred, making sure we return that
    * fact to the caller, by throwing "intelligent" exception,
    * in addition to logging error locally.
    */
   log.info:x:@.arguments/*/message
   throw:Access denied
      public:true
      status:401


/*
 * Truncates all tables in the currently open database connection.
 *
 * Assumes an open database connection to the database you want to
 * truncate all tables in.
 *
 * This allows you to create a database transaction, for things such
 * restoring backups, truncating old records, ensuring no changes are
 * done, unless backup is successfully restored, etc.
 */
slots.create:magic.db.mysql.truncate


   /*
    * Listing all tables in currently open database connection.
    */
   mysql.select:"show full tables where Table_Type = 'BASE TABLE'"

   /*
    * Making sure we end up with sane children node names, such that
    * we can easily traverse nodes further down.
    */
   remove-nodes:x:@mysql.select/*/*/Table_type
   set-name:x:@mysql.select/*/*
      .:table


   /*
    * Creating our SQL to truncate all tables in database.
    *
    * Turning OFF foreign key checks as the first part of our SQL.
    */
   .sql:"set foreign_key_checks = 0;"


   /*
    * Running through each table in database and dynamically building our SQL.
    */
   for-each:x:@mysql.select/*/*/table
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:"truncate `"
            get-value:x:@.dp/#
            .:"`;"


   /*
    * Turning ON foreign key checks again.
    */
   set-value:x:@.sql
      strings.concat
         get-value:x:@.sql
         .:"set foreign_key_checks = 1;"


   /*
    * Executing SQL.
    */
   mysql.execute:x:@.sql


   /*
    * Returning success to caller.
    */
   return-value:bool:true


/*
 * Lists all database in your MySQL instance.
 */
slots.create:magic.db.mysql.databases


   /*
    * Opens up our connection
    */
   .connection:[information_schema]
   if
      and
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .
         not
            eq
               get-value:x:@.arguments/*/connection-string
               .:
      .lambda
         set-value:x:@.connection
            strings.concat
               .:[
               get-value:x:@.arguments/*/connection-string
               .:|
               .:information_schema]
   mysql.connect:x:@.connection


      /*
       * Runs our query
       */
      mysql.select:show databases


      /*
       * Removes "system" databases.
       */
      remove-nodes:x:@mysql.select/*/*/=information_schema/.
      remove-nodes:x:@mysql.select/*/*/=mysql/.
      remove-nodes:x:@mysql.select/*/*/=performance_schema/.
      remove-nodes:x:@mysql.select/*/*/=sys/.


      /*
       * Renaming "Database" nodes.
       */
      set-name:x:@mysql.select/*/*
         .:db


      /*
       * Returns the remaining databases to caller.
       */
      return-nodes:x:@mysql.select/*

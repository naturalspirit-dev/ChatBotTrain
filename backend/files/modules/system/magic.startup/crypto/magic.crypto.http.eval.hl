
/*
 * Allows you to invoke an HTTP REST GET method on another server,
 * cryptographically signing your payload before it's sent, with your
 * server's private key.
 *
 * Arguments.
 *
 * - [url] - The URL of your invocation
 * - [.lambda] - The lambda object to evaluate at the other side
 */
slots.create:magic.crypto.http.eval


   /*
    * Sanity checking invocation.
    */
   validators.mandatory:x:@.arguments/*/.lambda
   validators.url:x:@.arguments/*/url


   /*
    * Loading server private key since we'll need to sign the payload.
    */
   .private-key
   set-value:x:@.private-key
      io.file.load:/modules/system/crypto/keys/private-key.key


   /*
    * Retrieving server's public key, and its fingerprint.
    */
   signal:magic.crypto.get-server-public-key


   /*
    * Creating Hyperlambda payload, and signing
    * the message with our private key.
    */
   lambda2hyper:x:@.arguments/*/.lambda/*
   crypto.sign:x:@lambda2hyper
      raw:true
      signing-key:x:@.private-key
      signing-key-fingerprint:x:@signal/#/*/fingerprint


   /*
    * Invoking HTTP REST endpoint with signed payload.
    */
   http.patch:x:@.arguments/*/url
      headers
         Content-Type:application/octet-stream
      payload:x:@crypto.sign


   /*
    * Returning result to caller.
    */
   unwrap:x:./*/return
   add:x:./*/return
      get-nodes:x:@http.patch/*/headers
   add:x:./*/return
      get-nodes:x:@http.patch/*/content
   return:x:@http.patch

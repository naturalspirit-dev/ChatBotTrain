
/*
 * Returns number of log items in the database.
 */
.arguments
   query:string
.description:"Returns the number of log items in the log database."


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Retrieves items from our database, according to which database is
 * the default database and database type in the system.
 */
config.get:"magic:logging:database"
config.get:"magic:databases:default"
set-name:x:../**/xxx.connect
   strings.concat
      get-value:x:@config.get
      .:.connect
set-name:x:../**/xxx.read
   strings.concat
      get-value:x:@config.get
      .:.read


/*
 * Adding filter argument, if given.
 */
if
   exists:x:@.arguments/*/query
   .lambda
      strings.concat
         get-value:x:@.arguments/*/query
         .:%
      unwrap:x:+/*/*/*/*
      add:x:../**/table/.
         .
            where
               or
                  type.eq:x:@.arguments/*/query
                  content.like:x:@strings.concat


/*
 * Retrieving count from database.
 */
unwrap:x:+
xxx.connect:x:@config.get/@config.get
   xxx.read
      table:log_entries
      columns
         count(*) as count

   /*
    * Returning log items to caller.
    */
   unwrap:x:+/*
   return
      result:x:./-/-/*/*

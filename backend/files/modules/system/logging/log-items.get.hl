
/*
 * Returns log items in the database according to specified arguments.
 */
.arguments
   offset:long
   limit:long
   query:string
.description:"Returns log items according to the specified arguments."


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Retrieves items from our database, according to which database is
 * the default database and database type in the system.
 */
config.get:"magic:logging:database"
config.get:"magic:databases:default"
strings.concat
   .:magic.db.
   get-value:x:@config.get
   .:.read


/*
 * Adding paging arguments.
 */
add:x:../*/wait.signal/*/args
   get-nodes:x:@.arguments/*/limit
   get-nodes:x:@.arguments/*/offset


/*
 * Adding filter argument, if given.
 */
if
   exists:x:@.arguments/*/query
   .lambda
      strings.concat
         get-value:x:@.arguments/*/query
         .:%
      unwrap:x:+/*/*/*
      add:x:../*/wait.signal/*/args
         .
            or
               type.eq:x:@.arguments/*/query
               content.like:x:@strings.concat


/*
 * Retrieving items from database.
 */
unwrap:x:+/*
wait.signal:x:@strings.concat
   database:x:@config.get/@config.get
   table:log_entries
   args
      order:id
      direction:desc


/*
 * Returning items to caller.
 */
add:x:+
   get-nodes:x:@wait.signal/*
return-nodes

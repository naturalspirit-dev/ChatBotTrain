
/*
 * Returns log items in the database according to specified arguments.
 */
.arguments
   offset:long
   limit:long
   query:string
.description:"Returns log items according to the specified arguments."


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Adding paging arguments.
 */
add:x:../*/xxx
   get-nodes:x:@.arguments/*/limit
   get-nodes:x:@.arguments/*/offset


/*
 * Retrieves items from our database, according to which database is
 * the default database and database type in the system.
 */
config.get:"magic:databases:default"
set-name:x:./*/xxx.connect
   strings.concat
      get-value:x:@config.get
      .:.connect
set-name:x:./**/xxx.read
   strings.concat
      get-value:x:@config.get
      .:.read


/*
 * Adding filter argument, if given.
 */
if
   exists:x:@.arguments/*/query
   .lambda
      strings.concat
         get-value:x:@.arguments/*/query
         .:%
      unwrap:x:+/*/*/*/*
      add:x:../**/table/.
         .
            where
               or
                  type.eq:x:@.arguments/*/query
                  content.like:x:@strings.concat


/*
 * Retrieving items from database.
 */
unwrap:x:+
xxx.connect:magic
   add:x:+
      get-nodes:x:@.arguments/*/offset
      get-nodes:x:@.arguments/*/limit
   xxx.read
      table:log_entries
      order:id
      direction:desc

   /*
    * Returning items to caller.
    */
   return-nodes:x:-/*


// Returns number of log items in the database.
.arguments
   query:string
.description:Returns the number of log items in your database matching the optional filter

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Adding filter argument, if given.
if
   exists:x:@.arguments/*/query
   .lambda
      strings.concat
         get-value:x:@.arguments/*/query
         .:%
      unwrap:x:+/*/*/*/*
      add:x:../*/data.connect/*/data.read
         .
            where
               or
                  type.eq:x:@.arguments/*/query
                  content.like:x:@strings.concat

// Retrieving count from database.
data.connect:magic
   data.read
      table:log_entries
      columns
         count(*) as count

   // Returning log items to caller.
   unwrap:x:+/*
   return
      count:x:@data.read/*/*


/*
 * Returns log item count grouped by type of item.
 */
.description:"Returns log item count grouped by type of item."
.arguments
   filter:string


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Adding filter argument, if given.
 */
if
   exists:x:@.arguments/*/filter
   .lambda
      strings.concat
         get-value:x:@.arguments/*/filter
         .:%
      unwrap:x:+/*/*/*/*
      add:x:../*/data.connect/*/data.read
         .
            where
               or
                  type.eq:x:@.arguments/*/filter
                  content.like:x:@strings.concat


/*
 * Making sure we're adding the correct date conversion,
 * according to what database type server is using.
 */
config.get:"magic:databases:default"
switch:x:@config.get
   case:mysql
      add:x:../**/columns
         .
            date_format(created, '%Y-%m-%d') as date
      add:x:../**/group
         .
            date
   case:mssql
      add:x:../**/columns
         .
            convert(char(10), created, 126) as date
      add:x:../**/group
         .
            convert(char(10), created, 126)


/*
 * Retrieving statistics from database.
 */
data.connect:magic
   data.read
      table:log_entries
      limit:10
      columns
         count(*) as count
      group
      order:date
      direction:asc

   /*
    * Making sure we return empty dates, as in dates without
    * any log entries.
    */
   .no:int:13
   while
      mte
         get-value:x:@.no
         .:int:0
      .lambda
         date.now
         math.subtract:x:@date.now
            time
               days:x:@.no
         date.format:x:-
            format:yyyy-MM-dd

         // Checking if SQL returned currently iterated date.
         strings.concat
            .:././././0/*/*/date/=
            get-value:x:@date.format
         set-x:x:./*/if/*/exists
            convert:x:@strings.concat
               type:x
         if
            exists
            .lambda
               strings.concat
                  .:././././././0/*/*/date/=
                  get-value:x:@date.format
                  .:/.
               set-x:x:+/*/get-nodes
                  convert:x:@strings.concat
                     type:x
               add:x:../*/return
                  get-nodes
         else
            unwrap:x:+/*/*/*
            add:x:../*/return
               .
                  .
                     date:x:@date.format
                     count:long:0
         math.decrement:x:@.no


/*
 * Returning items to caller.
 */
return
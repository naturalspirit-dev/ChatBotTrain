
/*
 * Returns all endpoints in the system, with the associated authorization requirements,
 * as a list of roles.
 */
.description:"Returns all HTTP endpoints in the system, with the associated authorization requirements, as a list of [roles]."


/*
 * Declares how many seconds the operation should be cached.
 */
.cache-seconds:int:300


/*
 * Invokes [endpoints.list], making sure we cache the results, since it's a
 * fairly expensive operation.
 */
cache.try-get:authorizations
   expiration:x:@.cache-seconds
   expiration-type:absolute
   .lambda

      /*
       * Cache miss, retrieving endpoints, and returning to caller.
       */
      .result
      endpoints.list
      for-each:x:@endpoints.list/*
         add:x:+/+/*/*/*/auth
            get-nodes:x:@.dp/#/*/auth/*
         unwrap:x:+/*/*/*
         add:x:@.result
            .
               .
                  path:x:@.dp/#/*/path
                  verb:x:@.dp/#/*/verb
                  auth
      return:x:@.result/*


/*
 * Making sure we cache result such that HTTP clients won't try to retrieve
 * value from server, more frequently than our Cache-Control header declares
 * as feasable.
 */
strings.concat
   .:public, max-age=
   get-value:x:@.cache-seconds
response.headers.add
   Cache-Control:x:@strings.concat


/*
 * Returning results of above invocation to caller.
 */
add:x:../*/return
   get-nodes:x:@cache.try-get/#/*
return

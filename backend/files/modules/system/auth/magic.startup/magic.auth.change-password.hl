
/*
 * Changes the password of the currently authenticated user.
 *
 * Requires the user to be successfully authenticated, and an [.arguments]/[password]
 * being the new password to use for authenticated user.
 *
 * Optionally pass in an explicit [database-type] for cases when your database hasn't been configured yet.
 */
slots.create:magic.auth.change-password


   /*
    * Ensuring a password was actually provided.
    */
   if
      or
         eq
            get-value:x:@.arguments/*/password
            .:
         eq
            get-value:x:@.arguments/*/password
            .
      .lambda
         throw:You have to provide a new password


   /*
    * Retrieving currently authenticated user's username, and sanity checking to make sure
    * client actually is authenticated.
    */
   auth.ticket.get
   if
      or
         eq
            get-value:x:@auth.ticket.get
            .
         eq
            get-value:x:@auth.ticket.get
            .:
      .lambda
         throw:You have to be authenticated in order to change your password


   /*
    * Hashing password.
    */
   crypto.password.hash:x:@.arguments/*/password


   /*
    * Opens up our database connection.
    * Notice, since this slot is used during setup of system, we allow for caller to
    * explicitly change database type during invocation.
    */
   add:x:+
      get-nodes:x:@.arguments/*/database-type
   data.connect:magic


      /*
       * Changes the password of the currently logged in user to whatever new password user provided.
       */
      add:x:+
         get-nodes:x:@.arguments/*/database-type
      data.update
         table:users
         values
            password:x:@crypto.password.hash
         where
            and
               username:x:@auth.ticket.get

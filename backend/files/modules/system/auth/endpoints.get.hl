
/*
 * Returns all dynamic HTTP endpoints in the system,
 * with their associated verb(s), and roles that are
 * allowed to access the endpoint.
 */
.description:Returns all dynamic HTTP endpoints in the system, with their associated verb(s), and roles that are allowed to access the endpoint


/*
 * Declares how many seconds the operation should be cached.
 */
.cache-seconds:int:300


/*
 * Invokes [endpoints.list], making sure we cache the results, since it's a
 * fairly expensive operation.
 */
cache.try-get:magic.auth.endpoints
   expiration:x:@.cache-seconds
   .lambda

      /*
       * Cache miss, retrieving endpoints, and returning to caller.
       */
      .result
      endpoints.list
      for-each:x:@endpoints.list/*
         add:x:+/+/*/*/*/auth
            get-nodes:x:@.dp/#/*/auth/*
         unwrap:x:+/*/*/*
         add:x:@.result
            .
               .
                  path:x:@.dp/#/*/path
                  verb:x:@.dp/#/*/verb
                  auth
      return:x:@.result/*


/*
 * Returning results of above invocation to caller.
 */
add:x:../*/return
   get-nodes:x:@cache.try-get/#/*
return

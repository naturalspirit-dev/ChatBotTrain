
/*
 * Creates a user and inserts into the "magic" database,
 * with the username/password specified in the [.arguments] collection,
 * and belonging to the specified [roles] from the [.arguments] collection.
 *
 * Password is cryptographically hashed using the blow fish hashing algorithm,
 * and cryptographically and securely stored with a per-record based salt in the
 * "users" table.
 */


/*
 * Hashing password.
 */
crypto.password.hash:x:@.arguments/*/password


/*
 * Opening up a database connection to magic.
 */
data.connect:magic
   database-type:x:@.arguments/*/database-type


   /*
    * Inserting user.
    */
   data.execute:insert into users (username, password) values (@username, @password);
      @username:x:@.arguments/*/username
      @password:x:@crypto.password.hash
      database-type:x:@.arguments/*/database-type


   /*
    * Inserting all roles.
    */
   for-each:x:@.arguments/*/roles/*


      /*
       * Then inserting into users_roles an association between user and role.
       */
      data.execute:insert into users_roles (role, user) values (@role, @user)
         @role:x:@.dp/#
         @user:x:@.arguments/*/username
         database-type:x:@.arguments/*/database-type


/*
 * Spawns a new terminal on server.
 */
.arguments
   channel:string
   folder:string


/*
 * Verifying caller has access to execute file.
 */
auth.ticket.verify:root


/*
 * Logging the fact that we're starting a terminal on server.
 */
log.info
   .:"Starting terminal on server on channel '"
   get-value:x:@.arguments/*/channel
   .:"'"


/*
 * Figuring out channel name to transmit results to.
 */
strings.concat
   .:ide.terminal.out.
   get-value:x:@.arguments/*/channel


/*
 * Starting terminal.
 */
unwrap:x:+/*/*/.channel
system.terminal.create:x:@.arguments/*/channel
   folder:x:@.arguments/*/folder
   .stdOut
      .channel:x:@strings.concat
      unwrap:x:+/*/args/*/result
      sockets.signal:x:@.channel
         roles:root
         args
            result:x:@.arguments/*/cmd
   .stdErr
      .channel:x:@strings.concat
      unwrap:x:+/*/args/*/result
      sockets.signal:x:@.channel
         roles:root
         args
            error:bool:true
            result:x:@.arguments/*/cmd

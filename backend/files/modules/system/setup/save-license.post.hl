
/*
 * Saves a license specified by caller.
 */
.description:"Saves a license as specified by caller."
.arguments
   license:string


/*
 * Ensures user is authorized to access endpoint.
 */
auth.ticket.verify:root


/*
 * Loads the configuration, changes the current license, and saves it again.
 */
config.load
json2lambda:x:-
set-value:x:@json2lambda/*/magic/*/license
   get-value:x:@.arguments/*/license
lambda2json:x:@json2lambda/*
   format:true
config.save:x:@lambda2json


/*
 * Invoking slot responsible for applying license, if it was explicitly given,
 * making sure we give the above save invocation some time to finish up.
 *
 * By sleeping here, we make sure the configuration is updated before proceeding,
 * otherwise the next invocation which is invoked during setup, might for some
 * reasons end up using the old configuration.
 */
sleep:2000
license.apply


/*
 * Returning success to calller.
 */
return-nodes
   result:success

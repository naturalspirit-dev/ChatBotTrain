
/*
 * Endpoint for uploading CSV file with multiple users in.
 */
.description:Endpoint for uploading CSV file with multiple users in
.arguments
   file:*
.type:file-upload
.accept:multipart/form-data

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/file/*/name
validators.mandatory:x:@.arguments/*/file/*/stream

// Verifying user has access to invoke endpoint.
auth.ticket.verify:root

// Loading CSV content from specified stream.
io.stream.read:x:@.arguments/*/file/*/stream

// Transforming content to lambda.
csv2lambda:x:@io.stream.read

// Iterating through each record in above CSV file and creating a user belonging to the guest role.
data.connect:[generic|magic]

   for-each:x:@csv2lambda/*
      signal:transformers.hash-password
         reference:x:@.dp/#/*/password

      // Creating our record.
      data.create
         table:users
         return-id:bool:false
         values
            username:x:@.dp/#/*/username
            password:x:@.dp/#/*/password
      for-each:x:@.dp/#/*
         switch:x:@.dp/#
            case:username
            case:password
               // Do nothing
               .foo
            default
               get-name:x:@.dp/#
               data.create
                  table:users_extra
                  return-id:bool:false
                  values
                     user:x:@for-each/@.dp/#/*/username
                     type:x:@get-name
                     value:x:@.dp/#


// Returning success to caller.
return
   result:success

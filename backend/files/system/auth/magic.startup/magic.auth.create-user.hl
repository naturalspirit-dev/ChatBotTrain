
/*
 * Creates a user and inserts into the "magic" database,
 * with the username/password specified in the [.arguments] collection,
 * and belonging to the specified [roles] from the [.arguments] collection.
 * 
 * Password is cryptographically hashed using the blow fish hashing algorithm,
 * and cryptographically and securely stored with a per-record based salt in the
 * "users" table.
 */
slots.create:magic.auth.create-user

   // Hashing password.
   crypto.password.hash:x:@.arguments/*/password

   // Opening up a database connection to magic.
   data.connect:magic
      database-type:x:@.arguments/*/database-type

      // Inserting user.
      data.create
         database-type:x:@.arguments/*/database-type
         table:users
         values
            username:x:@.arguments/*/username
            password:x:@crypto.password.hash

      // Inserting all roles.
      for-each:x:@.arguments/*/roles/*

         // Then inserting into users_roles an association between user and role.
         data.create
            database-type:x:@.arguments/*/database-type
            table:users_roles
            values
               role:x:@.dp/#
               user:x:@.arguments/*/username

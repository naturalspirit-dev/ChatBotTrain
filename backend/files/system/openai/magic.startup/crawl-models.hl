
/*
 * Creates scheduled task that crawls all models periodically.
 */
try

   // Creating task that crawls all model periodically.
   tasks.create:ainiro-crawl-machine-learning-models
      description:Periodically crawls all models once every day to add new pages to model
      repeats:24.hours
      .lambda

         // Making sure exceptions never leave thread.
         try

            // Connecting to database.
            data.connect:[generic|magic]

               // Finding all types with a [base_url].
               data.read
                  table:ml_types
                  columns
                     type
                     base_url
                  where
                     and
                        base_url.neq

               // Looping through all types returned above.
               for-each:x:@data.read/*

                  // Doing some logging.
                  log.info:Crawling website
                     url:x:@.dp/#/*/base_url
                     type:x:@.dp/#/*/type

                  // Invoking slot doing the heavy lifting.
                  unwrap:x:./*/signal/*
                  unwrap:x:./*/signal/*/.onafter/**
                  signal:magic.ai.crawl-site
                     delay:int:2000
                     max:int:25
                     threshold:int:150
                     summarize:bool:true
                     url:x:@.dp/#/*/base_url
                     type:x:@.dp/#/*/type
                     .onafter
                        signal:magic.ai.vectorise
                           type:x:@.dp/#/*/type

         .catch

            // Logging.
            log.error:Could not crawl site
               error:x:@.arguments/*/message

.catch

   // Task already exists.
   console.log:Task to periodically crawl models already exists

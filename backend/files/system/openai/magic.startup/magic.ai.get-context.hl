
/*
 * Slot that returns context and references for the specified [type], given the
 * specified [threshold], the specified [prompt], the specified [vector_model]
 * and the specified [max_tokens].
 */
slots.create:magic.ai.get-context

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/type
   validators.mandatory:x:@.arguments/*/prompt
   validators.mandatory:x:@.arguments/*/threshold
   validators.mandatory:x:@.arguments/*/max_tokens
   validators.mandatory:x:@.arguments/*/vector_model
   validators.string:x:@.arguments/*/prompt
      min:1

   // Converting threshold in case we're given the wrong type.
   set-value:x:@.arguments/*/threshold
      convert:x:@.arguments/*/threshold
         type:double

   // Retrieving embeddings.
   .token
   set-value:x:@.token
      strings.concat
         .:"Bearer "
         config.get:"magic:openai:key"

   // Retrieving embedding for prompt.
   http.post:"https://api.openai.com/v1/embeddings"
      headers
         Authorization:x:@.token
         Content-Type:application/json
      payload
         input:x:@.arguments/*/prompt
         model:x:@.arguments/*/vector_model
      convert:true

   // Sanity checking above invocation.
   if
      not
         and
            mte:x:@http.post
               .:int:200
            lt:x:@http.post
               .:int:300
      .lambda

         // Oops, error - Logging error and returning OpenAI's HTTP status code to caller.
         lambda2hyper:x:@http.post
         log.error:Something went wrong while invoking OpenAI
            message:x:@http.post/*/content/*/error/*/message
            error:x:@lambda2hyper
         throw:x:@http.post/*/content/*/error/*/message
            public:bool:true
            status:x:@http.post

   // Fetching relevant snippets, making sure we profile the time it takes.
   .begin
   set-value:x:@.begin
      date.now

   // Contains all snippets that are above [threshold].
   .snippets

   // Condition for [while] loop, set to false when we have no more training snippets to inspect.
   .continue:bool:true

   // Offset in table to start fetching records from, incremented as we proceed through table.
   .offset:int:0

   // Looping through all training snippet records of specified type having embeddings.
   while:x:@.continue

      // Selecting 200 snippets from type.
      data.read
         table:ml_training_snippets
         columns
            embedding
            prompt
            completion
            uri
         where
            and
               embedding.neq
               type.eq:x:@.arguments/*/type
         limit:200
         offset:x:@.offset
         order:created

      // Looping through all records returned from above invocation.
      for-each:x:@data.read/*

         // Calculating dot product of currently iterated snippet's embedding and result from OpenAI for prompt.
         math.dot
            strings.split:x:@.dp/#/*/embedding
               .:,
            get-nodes:x:@http.post/*/content/*/data/0/*/embedding/*

         // Checking if dot product is higher than threshold.
         if
            mte:x:@math.dot
               get-value:x:@.arguments/*/threshold
            .lambda

               // Adding result to above [.snippets] collection.
               unwrap:x:+/*/*/*
               add:x:@.snippets
                  .
                     .
                        prompt:x:@.dp/#/*/prompt
                        completion:x:@.dp/#/*/completion
                        uri:x:@.dp/#/*/uri
                        dot:x:@math.dot

      // Sorting [.snippets] such that highest dot product comes first.
      sort:x:@.snippets/*
         if
            mt:x:@.lhs/#/*/dot
               get-value:x:@.rhs/#/*/dot
            .lambda
               set-value:x:@.result
                  .:int:-1
         else-if
            lt:x:@.lhs/#/*/dot
               get-value:x:@.rhs/#/*/dot
            .lambda
               set-value:x:@.result
                  .:int:1
         else
            set-value:x:@.result
               .:int:0

      /*
       * Updating [.snippets] now with the 50 top most relevant snippets.
       *
       * Notice, to make algorithm perform at least to some extent, we avoid
       * adding more than max 50 snippets to [.snippets].
       * These are the top 50 performing snippets according to vectors and
       * dot product and should suffice for most use cases.
       */
      remove-nodes:x:@.snippets/*
      add:x:@.snippets
         get-nodes:x:@sort/*/[0,50]

      // Checking if we're done.
      if
         lt
            get-count:x:@data.read/*
            .:int:200
         .lambda

            // We're done! We've found the top 50 most relevant snippets from our training material.
            set-value:x:@.continue
               .:bool:false

      else

         // Incrementing offset.
         math.increment:x:@.offset
            get-count:x:@data.read/*

   /*
    * Used to hold the referenced snippets used to create our context.
    *
    * Returned to caller as [snippets].
    */
   .result

   // Context value we return to caller.
   .context:

   // Temporary variable used to calculate tokens.
   .tmp:

   /*
    * Running through all [.snippets] creating our context
    * and storing referenced snippets in the process.
    */
   while
      exists:x:@.snippets/0
      .lambda

         /*
          * Concatenating top snippet to [.tmp] buffer
          * such that we can calculate total number of tokens
          * resulting from adding currently iterated snippet.
          */
         set-value:x:@.tmp
            strings.concat
               get-value:x:@.tmp
               .:"\r\n"
               get-value:x:@.snippets/0/*/prompt
               .:"\r\n"
               .:"\r\n"
               get-value:x:@.snippets/0/*/completion
               .:"\r\n"

         /*
          * Verifying that adding currently iterated snippet doesn't
          * produce more tokens than acceptable.
          *
          * Notice, even if currently iterated snippet might overflow threshold,
          * it might be possible to add the NEXT snippet, with lower dot product score,
          * if it is smaller.
          */
         if
            lt
               openai.tokenize:x:@.tmp
               get-value:x:@.arguments/*/max_tokens
            .lambda

               /*
                * We've got room for more context, hence updating [.context]
                * to the value of [.tmp].
                */
               set-value:x:@.context
                  get-value:x:@.tmp

               /*
                * Checking if we can use currently iterated snippet as a "reference",
                * which is only true if the snippet actually contains a [uri], and has
                * not already been added as a reference.
                */
               if
                  and
                     not-null:x:@.snippets/0/*/uri
                     not-exists:x:@.result/*/*/uri/={@.snippets/0/*/uri}
                  .lambda

                     // Adding current reference to [.result]
                     unwrap:x:+/*/*/*
                     add:x:@.result
                        .
                           .
                              prompt:x:@.snippets/0/*/prompt
                              uri:x:@.snippets/0/*/uri
         else

            /*
             * Removing the currently appended snippet from [.tmp]
             * to allow the logic to correctly check if we can use the NEXT snippet.
             */
            set-value:x:@.tmp
               get-value:x:@.context

         /*
          * Removing currently iterated snippet.
          *
          * Notice, this logic allows us to add smaller snippets with "less accuracy"
          * than larger snippets found as "better matches" since it will loop through
          * all 50 snippets we found above, and add "whatever it can add" to the context.
          */
         remove-nodes:x:@.snippets/0

   // Measuring how much time we spent looping through snippets.
   .time
   set-value:x:@.time
      math.subtract
         date.now
         get-value:x:@.begin
   set-value:x:@.time
      time.format:x:@.time
         format:"ss\\.fff"

   // Trimming [.context] before we return it to caller.
   set-value:x:@.context
      strings.trim:x:@.context
         .:"\r\n \t"

   // Returning context and relevant snippets to caller.
   add:x:./*/return/*/snippets
      get-nodes:x:@.result/*
   unwrap:x:./*/return/*
   return
      context:x:@.context
      db_time:x:@.time
      snippets

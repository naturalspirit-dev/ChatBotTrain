
/*
 * Twilio webhook callback allowing you to configure Twilio to send your backend SMS messages when your Twilio account receives messages
 */
.description:Twilio webhook callback allowing you to configure Twilio to send your backend SMS messages when your Twilio account receives messages

// Sanity checking invocation making sure invocation is using configured account ID from Twilio configuration.
if
   neq
      config.get:"magic:twilio:account_id"
      get-value:x:@.arguments/*/AccountSid
  .lambda

     // Oops ...!
     throw:Not authorised
        status:int:401

// Retrieving actual message.
.msg
set-value:x:@.msg
   get-value:x:@.arguments/*/Body

// Retrieving from value
.from
set-value:x:@.from
   get-value:x:@.arguments/*/From

// Basic logging.
log.info:Webhook invoked from Twilio
   from:x:@.from
   message:x:@.msg

// Lambda object to execute with callback.
.exe
   signal:foo

// Dynamically creating our lambda [.exe] callback by parametrising with [.arguments] given by Twilio.
set-value:x:@.exe/*/signal
   config.get:"magic:twilio:callback"

// Sanity checking configuration.
validators.mandatory:x:@.exe/*/signal
unwrap:x:+/*/*
add:x:@.exe/0
   .
      from:x:@.from
      message:x:@.msg

// Executing callback.
eval:x:@.exe

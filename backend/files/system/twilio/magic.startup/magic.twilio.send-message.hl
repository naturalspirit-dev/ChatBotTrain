
/*
 * Slot for sending a message using Twilio.
 */
slots.create:magic.twilio.send-message

   // Sanity checking invocation.
   validators.mandatory:x:@.arguments/*/to
   validators.mandatory:x:@.arguments/*/from
   validators.mandatory:x:@.arguments/*/body
   validators.mandatory:x:@.arguments/*/type

   // Getting Twilio account ID and SID from ml_type database.
   .account_id
   .sid
   data.connect:[generic|magic]
      data.read
         table:ml_type
         columns
            twilio_account_id
            twilio_account_sid
         where
            and
               type.eq:x:@.arguments/*/type
   set-value:x:@.account_id
      get-value:x:@data.read/*/*/twilio_account_id
   set-value:x:@.sid
      get-value:x:@data.read/*/*/twilio_account_sid

   // Creating Basic auth header.
   .auth
   set-value:x:@.auth
      strings.concat
         get-value:x:@.account_id
         .:":"
         get-value:x:@.sid
   set-value:x:@.auth
      convert:x:@.auth
         type:bytes
   set-value:x:@.auth
      strings.concat
         .:"Basic "
         convert:x:@.auth
            type:base64

   // Creating our URL to invoke Twilio API.
   .url
   set-value:x:@.url
      strings.concat
         .:"https://api.twilio.com/2010-04-01/Accounts/"
         get-value:x:@.account_id
         .:"/Messages.json"

   // Invoking Twilio.
   unwrap:x:./*/http.post/**
   http.post:x:@.url
      convert:true
      headers
         Content-Type:application/x-www-form-urlencoded
         Authorization:x:@.auth
      payload
         Body:x:@.arguments/*/body
         From:x:@.arguments/*/from
         To:x:@.arguments/*/to

   // Making sure above invocation succeeded.
   if
      not
         and
            mte:x:@http.post
               .:int:200
            lt:x:@http.post
               .:int:300
      .lambda

         // Something went wrong while invoking Twilio
         lambda2hyper:x:@http.post/*/content
         log.error:Something went wrong as we invoked Twilio
            response:x:@lambda2hyper
         throw:Something went wrong as we invoked Twilio, check your log for details

   // Logging success.
   log.info:Successfully sent message using Twilio
      to:x:@.arguments/*/to
      from:x:@.arguments/*/from
      body:x:@.arguments/*/body


// Generates a frontend for you.
.description:Generates a frontend for you according to the given arguments
.arguments
   templateName:string
   apiUrl:string
   frontendUrl:string
   email:string
   name:string
   copyright:string
   endpoints:*
   deployLocally:bool
   args:*

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/email
validators.email:x:@.arguments/*/email
validators.mandatory:x:@.arguments/*/apiUrl
validators.url:x:@.arguments/*/apiUrl

// Figuring out which template/generator to use, and invoking it.
strings.concat
   .:/misc/templates/
   .:x:@.arguments/*/templateName
   .:/generate.hl
add:x:+/+/+
   get-nodes:x:@.arguments/*/endpoints
add:x:+/+
   get-nodes:x:@.arguments/*/args
unwrap:x:+/*
io.file.execute:x:@strings.concat
   apiUrl:x:@.arguments/*/apiUrl
   frontendUrl:x:@.arguments/*/frontendUrl
   email:x:@.arguments/*/email
   name:x:@.arguments/*/name
   copyright:x:@.arguments/*/copyright

// Creating a log entry with LOC that was generated.
.no:int:0
for-each:x:@io.file.execute/*
   strings.split:x:@.dp/#/*
      .:"\n"
   math.increment:x:@.no
      get-count:x:@strings.split/*
strings.concat
   .:"LOC generated '"
   get-value:x:@.no
   .:"' for type 'frontend'"
   .:" with name '"
   get-value:x:@.arguments/*/name
   .:"'"
log.info:x:@strings.concat

/*
 * Creating a ZIP stream from return value of above [io.file.execute]
 * and returning it to client.
 */
add:x:../*/io.content.zip-stream
   get-nodes:x:@io.file.execute/*
io.content.zip-stream
if
   not
      get-value:x:@.arguments/*/deployLocally
   .lambda

      /*
       * Caller wants a ZIP file returned, making sure we get HTTP headers correctly applied.
       */
      strings.concat
         .:"attachment; filename=\""
         get-value:x:@.arguments/*/name
         .:".zip\""
      response.headers.set
         Content-Type:application/zip
         Content-Disposition:x:@strings.concat
         Access-Control-Expose-Headers:Content-Disposition
      return-value:x:@io.content.zip-stream


/*
 * Caller wants to deploy frontend locally on server.
 *
 * First we'll need to verify that "frontends" folder exists within "/etc/".
 */
if
   not
      io.file.exists:/etc/frontends/
   .lambda

      // Creating "frontend" folder.
      io.folder.create:/etc/frontends/
else

   // Checking if folder for app already exists, and if so, making sure we delete it.
   strings.concat
      .:/etc/frontends/
      get-value:x:@.arguments/*/name
   if
      io.folder.exists:x:@strings.concat
      .lambda
         io.folder.delete:x:@strings.concat

// Then we need to construct a complete path for ZIP file.
strings.concat
   .:/etc/frontends/
   get-value:x:@.arguments/*/name
   .:.zip

// Checking if ZIP file already exists, and if so, deleting it.
if
   io.file.exists:x:@strings.concat
   .lambda
      io.file.delete:x:@strings.concat

// Saving ZIP stream.
io.stream.save-file:x:@strings.concat
   get-value:x:@io.content.zip-stream

/*
 * Unzipping file in its current folder and returning success to caller,
 * making sure we delete file in the process.
 */
io.file.unzip:x:@strings.concat
io.file.delete:x:@strings.concat
return
   result:success


/*
 * Add the specified column to your table.
 * 
 * Arguments;
 * - [databaseType] - Database type, typically 'mssql' or 'mysql'.
 * - [connectionString] - Connection string to use
 * - [databaseName] - Name of database
 * - [tableName] - Name of table
 * - [columnName] - Name of column
 * - [columnType] - Type declaration of column
 * - [foreignTable] - Table column links to
 * - [foreignField] - Field column links to
 * - [generate] - If true will not execute SQL but only return DDL to caller
 */
.arguments
   databaseType:string
   connectionString:string
   databaseName:string
   tableName:string
   columnName:string
   columnType:string
   columnLength:int
   defaultValue:string
   foreignTable:string
   foreignField:string
   generate:bool
.description:Adds the specified column to your specified table
.type:internal

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/connectionString
validators.mandatory:x:@.arguments/*/databaseName
validators.mandatory:x:@.arguments/*/tableName
validators.mandatory:x:@.arguments/*/columnName
validators.mandatory:x:@.arguments/*/columnType
validators.mandatory:x:@.arguments/*/databaseType
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:sqlite
   .:pgsql
   .:mssql

// SQL that executes to modify DDL.
.sql
set-value:x:@.sql
   strings.concat
      .:"alter table "
      get-value:x:@.arguments/*/tableName
      .:" add column "
      get-value:x:@.arguments/*/columnName
      .:" "
      get-value:x:@.arguments/*/columnType
      .:(
      get-value:x:@.arguments/*/columnLength
      .:)
if
   and
      exists:x:@.arguments/*/defaultValue
      neq:x:@.arguments/*/defaultValue
         .
   .lambda
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:" default "
            get-value:x:@.arguments/*/defaultValue
if
   and
      not-null:x:@.arguments/*/foreignTable
      not-null:x:@.arguments/*/foreignField
   .lambda
      set-value:x:@.sql
         strings.concat
            get-value:x:@.sql
            .:" references "
            get-value:x:@.arguments/*/foreignTable
            .:"("
            get-value:x:@.arguments/*/foreignField
            .:")"

// Appending semi colon at end.
set-value:x:@.sql
   strings.concat
      get-value:x:@.sql
      .:;

// Checking if caller only wants to generate SQL statement.
if:x:@.arguments/*/generate
   unwrap:x:+/*
   return
      sql:x:@.sql


// Opening our database connection.
strings.concat
   .:[
   get-value:x:@.arguments/*/connectionString
   .:|
   get-value:x:@.arguments/*/databaseName
   .:]
data.connect:x:-
   database-type:x:@.arguments/*/databaseType
   data.execute:x:@.sql
      database-type:x:@.arguments/*/databaseType


// Flushing database cache.
strings.concat
   .:magic.sql.databases.
   get-value:x:@.arguments/*/databaseType
   :.
   get-value:x:@.arguments/*/connectionString
cache.set:x:-

// Returning result to caller.
unwrap:x:+/*
return
   result:success
   sql:x:@.sql

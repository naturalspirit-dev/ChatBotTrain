
/*
 * Add the specified column to your table.
 * 
 * Arguments;
 * - [databaseType] - Database type, typically 'mssql' or 'mysql'.
 * - [connectionString] - Connection string to use
 * - [databaseName] - Name of database
 * - [tableName] - Name of table
 * - [columnName] - Name of column
 * - [columnType] - Type declaration of column
 */
.arguments
   databaseType:string
   connectionString:string
   databaseName:string
   tableName:string
   columnName:string
   columnType:string
   defaultValue:string
.description:Adds the specified column to your specified table
.type:internal

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/connectionString
validators.mandatory:x:@.arguments/*/databaseName
validators.mandatory:x:@.arguments/*/tableName
validators.mandatory:x:@.arguments/*/columnName
validators.mandatory:x:@.arguments/*/columnType
validators.mandatory:x:@.arguments/*/databaseType
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:sqlite

// Creating our database connection.
strings.concat
   .:[
   get-value:x:@.arguments/*/connectionString
   .:|
   get-value:x:@.arguments/*/databaseName
   .:]
data.connect:x:-
   database-type:x:@.arguments/*/databaseType

   // Creating our correct SQL according to [databaseType].
   switch:x:@.arguments/*/databaseType

      case:mysql
         .sql
         set-value:x:@.sql
            strings.concat
               .:"alter table "
               get-value:x:@.arguments/*/tableName
               .:" add column "
               get-value:x:@.arguments/*/columnName
               .:" "
               get-value:x:@.arguments/*/columnType
         if
            and
               exists:x:@.arguments/*/defaultValue
               neq:x:@.arguments/*/defaultValue
                  .
            .lambda
               set-value:x:@.sql
                  strings.concat
                     get-value:x:@.sql
                     .:" default "
                     get-value:x:@.arguments/*/defaultValue
         data.execute:x:@.sql
            database-type:x:@.arguments/*/databaseType

      case:sqlite
         .sql
         set-value:x:@.sql
            strings.concat
               .:"alter table "
               get-value:x:@.arguments/*/tableName
               .:" add column "
               get-value:x:@.arguments/*/columnName
               .:" "
               get-value:x:@.arguments/*/columnType
         if
            and
               exists:x:@.arguments/*/defaultValue
               neq:x:@.arguments/*/defaultValue
                  .
            .lambda
               set-value:x:@.sql
                  strings.concat
                     get-value:x:@.sql
                     .:" default "
                     get-value:x:@.arguments/*/defaultValue
         log.info:x:@.sql
         data.execute:x:@.sql
            database-type:x:@.arguments/*/databaseType

// Flushing database cache.
strings.concat
   .:magic.sql.databases.
   get-value:x:@.arguments/*/databaseType
   :.
   get-value:x:@.arguments/*/connectionString
cache.set:x:-

// Returning result to caller.
return
   result:success

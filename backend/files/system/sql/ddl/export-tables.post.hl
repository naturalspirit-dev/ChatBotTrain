
/*
 * Exports all the specified tables and returns to caller as DDL.
 *
 * Arguments;
 * - [databaseType] - Database type, typically 'mssql' or 'mysql'.
 * - [connectionString] - Connection string to use
 * - [databaseName] - Name of database
 * - [tables] - List of tables to export
 * - [full] - If true will generate "create database" parts of script
 */
.arguments
   databaseType:string
   connectionString:string
   databaseName:string
   tables:*
   full:bool
.description:Exports all the specified tables and returns to caller as DDL
.type:internal

// Ensures user is authorized to access endpoint.
auth.ticket.verify:root

// Sanity checking invocation.
validators.mandatory:x:@.arguments/*/connectionString
validators.mandatory:x:@.arguments/*/databaseName
validators.mandatory:x:@.arguments/*/tables
validators.mandatory:x:@.arguments/*/databaseType
validators.enum:x:@.arguments/*/databaseType
   .:mysql
   .:sqlite

// Creating our database connection.
strings.concat
   .:[
   get-value:x:@.arguments/*/connectionString
   .:|
   get-value:x:@.arguments/*/databaseName
   .:]
data.connect:x:-
   database-type:x:@.arguments/*/databaseType

   // Creating our correct SQL according to [databaseType].
   switch:x:@.arguments/*/databaseType

      case:sqlite
         throw:SQLite is currently not supported
            public:true
            status:400

      case:mysql
         .result
         set-value:x:@.result
            strings.concat
               .:"/*\r\n * Automatically generated by Magic.\r\n */\r\n"
         if:x:@.arguments/*/full
            set-value:x:@.result
               strings.concat
                  get-value:x:@.result
                  .:"CREATE DATABASE "
                  get-value:x:@.arguments/*/databaseName
                  .:";\r\nUSE "
                  get-value:x:@.arguments/*/databaseName
                  .:";\r\nSET foreign_key_checks = 0;\r\n\r\n"
         for-each:x:@.arguments/*/tables/*
            .sql
            set-value:x:@.sql
               strings.concat
                  .:"show create table "
                  get-value:x:@.dp/#
            data.select:x:@.sql
               database-type:x:@.arguments/*/databaseType
            set-value:x:@.result
               strings.concat
                  get-value:x:@.result
                  get-value:x:@data.select/*/1
                  .:";\r\n\r\n"
         if:x:@.arguments/*/full
            set-value:x:@.result
               strings.concat
                  get-value:x:@.result
                  .:"SET foreign_key_checks = 1;\r\n"
         unwrap:x:+/*
         return
            result:x:@.result
